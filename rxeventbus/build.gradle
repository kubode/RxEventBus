plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.6'
}

description 'Simple event bus using RxJava.'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    jcenter()
}

dependencies {
    compile 'io.reactivex:rxjava:1.1.5'
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.env.BINTRAY_USER ?: bintrayUser
    key = System.env.BINTRAY_KEY ?: bintrayKey
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = "${project.group}:${project.name}"
        version.name = project.version
    }
}
